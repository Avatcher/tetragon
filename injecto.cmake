set(INJECTO_RESOURCES_DIR ${CMAKE_SOURCE_DIR}/resources)
set(INJECTO_SYNTHETIC_DIR ${CMAKE_BINARY_DIR}/synthetic)
set(INJECTO_RESOURCE_PREFIX "RESOURCE_")

function(get_resource_macro Variable File)
   	set(MacroName ${INJECTO_RESOURCE_PREFIX})
   	string(APPEND MacroName ${File})
   	string(TOUPPER ${MacroName} MacroName)
   	string(REGEX REPLACE "[/\\.]" "_" MacroName ${MacroName})
   	set(${Variable} ${MacroName} PARENT_SCOPE)
endfunction()

function(generate_resource_macro MacroVariable File)
   	file(READ ${INJECTO_RESOURCES_DIR}/${File} FILE_TEXT)
   	string(APPEND Injection "${FILE_TEXT}")
   	string(REPLACE "\"" "\\\"" Injection "${Injection}")
   	string(REPLACE "\n" "\\n" Injection "${Injection}")
   	string(REPLACE "\t" "\\t" Injection "${Injection}")
   	string(PREPEND Injection "\"")
   	string(APPEND Injection "\"")
   	get_resource_macro(MacroName ${File})
   	set(${MacroName} ${Injection} PARENT_SCOPE)
   	set(${MacroVariable} ${MacroName} PARENT_SCOPE)
endfunction()

function(target_inject_resources Target GenerateFile)
	if(NOT TARGET ${Target})
		message(FATAL_ERROR "Could not find target '${Target}'")
	endif()
	file(GLOB_RECURSE Files "${INJECTO_RESOURCES_DIR}/**")
	foreach(File ${Files})
		string(REPLACE "${INJECTO_RESOURCES_DIR}/" "" File "${File}")
		generate_resource_macro(Macro ${File})
    	string(APPEND Definitions "#define ${Macro} ${${Macro}}\n")
   	endforeach()
   	set(BuiltFile "${INJECTO_SYNTHETIC_DIR}/${GenerateFile}")

   	string(TOUPPER "INJECTO_${Target}_${GenerateFile}" HeaderName)
	string(REGEX REPLACE "[/\\.]" "_" HeaderName "${HeaderName}")
	string(TIMESTAMP Datetime "%Y.%m.%d %H:%M:%S")

	file(MAKE_DIRECTORY ${INJECTO_SYNTHETIC_DIR})
	file(REMOVE ${BuiltFile})
   	file(TOUCH ${BuiltFile})
   	file(APPEND ${BuiltFile} "// This file was automatically generated\n")
   	file(APPEND ${BuiltFile} "// by Injecto. In case not all resources\n")
   	file(APPEND ${BuiltFile} "// are here, rerun CMake to generate this\n")
   	file(APPEND ${BuiltFile} "// file from scratch.\n")
	file(APPEND ${BuiltFile} "//\n")
   	file(APPEND ${BuiltFile} "// Created: ${Datetime}\n\n")
   	file(APPEND ${BuiltFile} "#ifndef ${HeaderName}\n")
   	file(APPEND ${BuiltFile} "#define ${HeaderName}\n\n")
   	file(APPEND ${BuiltFile} "${Definitions}\n")
   	file(APPEND ${BuiltFile} "#endif // ${HeaderName}\n")

   	target_include_directories(${Target} PRIVATE ${INJECTO_SYNTHETIC_DIR})
endfunction()
